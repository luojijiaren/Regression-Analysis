data ch11_bp;
  input x y;
  label x='age'
        y='Dbp';
cards;
  27   73
  21   66
  22   63
  24   75
  25   71
  23   70
  20   65
  20   70
  29   79
  24   72
  25   68
  28   67
  26   79
  38   91
  32   76
  33   69
  31   66
  34   73
  37   78
  38   87
  33   76
  35   79
  30   73
  31   80
  37   68
  39   75
  46   89
  49  101
  40   70
  42   72
  43   80
  46   83
  43   75
  44   71
  46   80
  47   96
  45   92
  49   80
  48   70
  40   90
  42   85
  55   76
  54   71
  57   99
  52   86
  53   79
  56   92
  52   85
  50   71
  59   90
  50   91
  52  100
  58   80
  57  109
;
run;
* Obtaining weights;
proc reg data=ch11_bp;
  model y = x;
  output out=temp r=residual;
run;
quit;
data temp;
  set temp;
  absr = abs(residual);
run;
proc reg data = temp ;
  model absr = x;
  output out = temp1 p = s ;
run;
quit;
data temp1;
  set temp1;
  w = 1/(s**2);
run;
* Obtain WLS fit;
proc reg data = temp1;
  weight w;
  model y = x / clb;
run;
quit;
* Another approach using OLS: gives a better sense of quality of fit;
data temp2;
 set temp1;
 int=sqrt(w);
 xw=x*int;
 yw=y*int;
run;
ods graphics on;
proc reg data = temp2 
plots=(diagnostics(stats=none) RStudentByLeverage(label) 
              CooksD(label) Residuals(smooth)
              DFFITS(label) DFBETAS ObservedByPredicted(label));
  model yw = int xw / noint clb;
run;
quit;
ods graphics off;



