data power_cell;
  input y x1 x2;
  label  y = 'cycles'
        x1 = 'Charge rate'
	    x2 = 'Temperature';
cards;
  150  0.6  10
   86  1.0  10
   49  1.4  10
  288  0.6  20
  157  1.0  20
  131  1.0  20
  184  1.0  20
  109  1.4  20
  279  0.6  30
  235  1.0  30
  224  1.4  30
;
run;
* Recoding the variables. ;
proc sql; 
  create table temp as
  select *, (x1-mean(x1))/.4 as lx1, (x2-mean(x2))/10 as lx2,
            ((x1-mean(x1))/.4)*((x1-mean(x1))/.4) as lx1sq, 
            ((x2-mean(x2))/10)*((x2-mean(x2))/10) as lx2sq, 
            ((x1-mean(x1))/.4)*((x2-mean(x2))/10) as lx1x2, 
            x1*x1 as x1sq, x2*x2 as x2sq, x1*x2 as x1x2
  from power_cell;
quit;
* Regression using original variables;
proc reg data = temp;
  model y = x1 x2 x1sq x2sq x1x2/ss1 lackfit;
run;
quit;
* See what the recoding has done to correlations;
proc corr data = temp;
  var x1 x1sq x2 x2sq lx1 lx1sq lx2 lx2sq;
run;
* And to the regression;
proc reg data = temp;
  model y = lx1 lx2 lx1sq lx2sq lx1x2/ss1 lackfit;
  output out=tempout p=fitted r=residual;
  test lx1sq=lx2sq=lx1x2=0;
run;
quit;
