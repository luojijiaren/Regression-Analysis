* BODY FAT EXAMPLE, KNN;

data bodyfat;
 *input triceps thigh midarm bodyfat;
 input x1 x2 x3 y;
 label x1='triceps'
       x2='thigh'
	   x3='midarm'
	   x4='body fat';
cards;
  19.5  43.1  29.1  11.9
  24.7  49.8  28.2  22.8
  30.7  51.9  37.0  18.7
  29.8  54.3  31.1  20.1
  19.1  42.2  30.9  12.9
  25.6  53.9  23.7  21.7
  31.4  58.5  27.6  27.1
  27.9  52.1  30.6  25.4
  22.1  49.9  23.2  21.3
  25.5  53.5  24.8  19.3
  31.1  56.6  30.0  25.4
  30.4  56.7  28.3  27.2
  18.7  46.5  23.0  11.7
  19.7  44.2  28.6  17.8
  14.6  42.7  21.3  12.8
  29.5  54.4  30.1  23.9
  27.7  55.3  25.7  22.6
  30.2  58.6  24.6  25.4
  22.7  48.2  27.1  14.8
  25.2  51.0  27.5  21.1
;
run;

/* 
THE EXTRA SS DECOMPOSITION IS DETERMINED BY THE ORDER IN WHICH THE
REGRESSORS ARE ENTERED, AS THE FIRST AND SECOND CALLS TO PROC REG
SHOW. OPTION SS1 TELLS SAS TO DO THE EXTRA SS DECOMPOSITION.  OPTION
PCORR1 TELLS SAS TO COMPUTE THE SQUARED PARTIAL CORRELATIONS IN THE
ORDER THE REGRESSORS ARE ENTERED. THE TEST STATEMENTS IMPLEMENT THE
GENERAL LINEAR TEST OF (1) beta2=beta3=0, (2) beta1=3, beta2=5, AND
(3) beta1=beta2 
*/

proc reg data=bodyfat;
 model y=x1 x2 x3/ss1 pcorr1;* This gets R^2_Y2|1 and R^2_Y3|12;
 thigh_midarm: test x2=0, x3=0;
 tr3_m5: test x1=3, x3=5;
 tr_eq_th: test x1-x2=0;
run;
quit;

* This gets R^2_Y1|2;
proc reg data=bodyfat;
 model y=x2 x1 x3/ss1 pcorr1;
run;
quit;

/*
SPECIFIC PARTIAL CORRELATIONS ARE OBTAINED FROM PROC CORR.
THIS ONE IS R_Y3.12
*/

proc corr data=bodyfat;
 var y;
 with x3;
 partial x1 x2;
run;


